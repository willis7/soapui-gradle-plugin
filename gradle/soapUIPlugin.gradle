targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_6

wrapper {
    gradleVersion = project.gradleApiVersion
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    /**
     * SoapUI dependencies.
     * <br/>
     *
     * TODO: must be tested and can be reverted in case wont luck...
     * Try use approach like SmartBear is using to solve jar-hell issue using for theirs maven plugin.
     * @see: http://smartbearsoftware.com/repository/maven2/com/smartbear/soapui/soapui-maven-plugin/5.3.0/soapui-maven-plugin-5.3.0.pom
     */
    compile "com.smartbear.soapui:soapui:$soapUIVersion", {
        exclude group: 'com.jgoodies', module: 'forms'
        exclude group: 'com.jgoodies', module: 'looks'
        exclude group: 'com.jgoodies', module: 'binding'
        exclude group: 'org.codehaus.groovy'    // SoapUI 5.3.0 uses groovy-all-2.1.7.jar
    }

    // see http://smartbearsoftware.com/repository/maven2/com/smartbear/soapui/soapui-pro-maven-plugin/5.1.2/soapui-pro-maven-plugin-5.1.2.pom
    compile "com.smartbear.soapui:soapui-pro:$soapUIVersion"
    compile 'opencsv:opencsv:1.8'
    compile 'jasperreports:jasperreports:4.0.2'
    compile 'net-components:netcomponents:1.3.8a'
    compile 'idw:idw:1.6.1'
    compile 'net.sourceforge.jexcelapi:jxl:2.6.12'

    // see https://github.com/willis7/soapui-gradle-plugin/issues/38
    compile 'org.reflections:reflections:0.9.9-RC1'

    runtime "com.google.code.findbugs:annotations:$findbugsVersion"
    runtime "com.google.code.findbugs:jsr305:$findbugsVersion"

    testCompile 'com.netflix.nebula:nebula-test:3.1.0', {
        exclude group: 'org.codehaus.groovy'
    }
}

configurations.all {
    resolutionStrategy {
        // force current SoapUI versions of dependencies + right spock version
        force "com.jgoodies:binding:$jgoodiesBindingVersion",
              "com.jgoodies:forms:$jgoodiesFormsVersion",
              "com.jgoodies:looks:$jgoodiesLooksVersion",
              'org.spockframework:spock-core:1.0-groovy-2.4'
    }
}

apply plugin: 'base'

defaultTasks 'build'
